//file:noinspection all

plugins {
    id "fabric-loom" version "1.4-SNAPSHOT"
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" version "1.9.20"
}

version = mod_version
group = maven_group

base {
    archivesName = mod_id
}

repositories {
    maven {
        name = "Terraformers"
        url  = "https://maven.terraformersmc.com/releases/"
    }

    maven {
        name = "Jitpack"
        url  = "https://jitpack.io/"
    }
}

loom {
    splitEnvironmentSourceSets()

    accessWidenerPath = file("src/main/resources/${mod_id}.accesswidener")

    mods {
        "$mod_id" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

    runs {
        // This adds a new gradle task that runs the datagen API: "gradlew runDatagen"
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
            vmArg "-Dfabric-api.datagen.modid=$mod_id"
 
            runDir "build/datagen"
        }
    }
}

// Add the generated resources to the main source set
sourceSets {
    main {
        resources {
            srcDirs += [
                    "src/generated/resources"
            ]
        }
    }
}
dependencies {
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "net.fabricmc:yarn:$minecraft_version+build.$yarn_build:v2"
    modImplementation "net.fabricmc:fabric-loader:$loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"
    modImplementation "net.fabricmc:fabric-language-kotlin:$fabric_kotlin_version"
    include modImplementation ("com.github.Chocohead:Fabric-ASM:$fabric_asm_version")

    modImplementation "com.terraformersmc:modmenu:$mod_menu_version"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = 17
    }
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
    }
}
